plugins {
    id 'java'
    id 'war'
    id 'net.researchgate.release' version '2.8.1'
    id "org.gretty" version "3.0.3"
    id "maven-publish"
    id "org.hidetake.ssh" version "2.10.1"
}

group 'com.github.simkuenzi'
version version

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation group: 'org.thymeleaf', name: 'thymeleaf', version: '3.0.11.RELEASE'
    implementation group: 'org.glassfish.jersey.core', name: 'jersey-server', version: '2.7'
    implementation group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet-core', version: '2.7'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

gretty {
    contextConfigFile = 'one-egg.xml'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/simkuenzi/one-egg")
            credentials {
                username = System.getenv("GIT_USER")
                password = System.getenv("GIT_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.web)
        }
    }
}

remotes {
    webServer {
        host = System.properties['deployServer']
        user = System.properties['deployUser']
        identity = file(System.properties['deployIdentity'] ?: 'id_rsa')
    }
}


task createProperties(dependsOn: processResources) {
    doLast {
        File d = new File("$buildDir/resources/main/com/github/simkuenzi/oneegg")
        d.mkdirs()
        new File(d, "version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

task deploy(dependsOn: war) {
    doLast {
        ssh.run {
            session(remotes.webServer) {
                put from: war.archivePath, into: System.properties['deployPath']
            }
        }
    }
}

classes {
    dependsOn createProperties
}

release {
    buildTasks = ['publish', 'deploy']
}